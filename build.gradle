plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
    // docker image builder
    id 'com.google.cloud.tools.jib' version "3.4.4"
    // coverage
    id 'jacoco'
    // code generation
    id "io.freefair.lombok" version "8.11"
}

sourceCompatibility = JavaVersion.VERSION_21

repositories {
    mavenCentral()
}

dependencies {
    // telegram
    implementation 'com.github.pengrad:java-telegram-bot-api:7.11.0'

    // database
    implementation 'jakarta.persistence:jakarta.persistence-api:3.2.0'
    implementation 'org.hibernate.orm:hibernate-core:7.1.0.Final'
    implementation 'org.hibernate.orm:hibernate-hikaricp:7.1.0.Final'
    implementation 'tech.ydb.dialects:hibernate-ydb-dialect:1.5.1'
    implementation 'tech.ydb.jdbc:ydb-jdbc-driver:2.3.20'

    // redis
    implementation 'redis.clients:jedis:5.0.0-beta2'

    // logging
    implementation 'org.slf4j:slf4j-api:2.0.5'
    implementation 'ch.qos.logback:logback-classic:1.4.12'

    // dependency injection
    implementation 'com.google.dagger:dagger:2.48'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.48'

    // utils
    implementation 'org.reflections:reflections:0.10.2'
    implementation 'org.junit.platform:junit-platform-commons:1.9.2'
    implementation 'org.apache.commons:commons-lang3:3.13.0'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.14.2'

    // tests
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testImplementation 'com.google.dagger:dagger:2.48'
    testAnnotationProcessor 'com.google.dagger:dagger-compiler:2.48'
}

application {
    // Define the main class for the application.
    mainClass = 'bookkeeper.telegram.App'
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report

    reports {
        csv.required = true
        html.required = false
    }
}

jib {
    container {
        // 256M should be enough
        jvmFlags = ["-Xmx256M"]
    }
}